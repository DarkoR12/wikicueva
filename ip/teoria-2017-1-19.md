<!-- TITLE: Examen Teórico - 19 de enero de 2017 -->

# Examen Teórico - 19 de enero de 2017

# Ejercicio 1

## Apartado A

> Disponemos de una secuencia S de enteros del segundo modelo de acceso secuencial en la que hay almacenados valores enteros. Diseñe un algoritmo que accediendo sólo una vez a cada elemento de la secuencia determine el número de inversiones adyacentes que tiene la secuencia. Una inversión adyacente es un par de elementos de la secuencia tales que se cumple que: \\( S_{i} \gt S_{i+1} \\).

Por ejemplo, en la secuencia S = (31, 5, 33, 45, 33, 15, 13, 15, 15), la solución es 4, correspondiente con las parejas (31, 5), (45, 33), (33, 15), (15, 13).

El truco en este ejercicio es dar la vuelta a la condición para ver que el elemento actual tiene que ser menor que el elemento anterior.

Guardamos el último elemento de la parte izquierda (la parte de la secuencia que ya hemos recorrido) en una variable `ultimo`, y la comparamos con `EA` para saber si tenemos una pareja o no.

```pascal
function NumInversionesAdyacentes(S : mse2) : integer;
var
	ultimo, parejas : integer;

begin
	iniciar_mse2(S);

	parejas := 0;

	if not esVacia_mse2(S) then begin
		avanzar_mse2(S);

		ultimo := ea_mse2(S);

		while not esUltimo_mse2(S) do begin
			avanzar_mse2(S);

			if ultimo > ea_mse2(S) then begin
				writeln('(', ultimo, ', ', ea_mse2(S), ')');

				parejas := parejas + 1;
			end;

			ultimo := ea_mse2(S);
		end;
	end;

	NumInversionesAdyacentes := parejas;
end;
```

## Apartado B

> Diseñe una función ExisteMultiploN que teniendo como parámetros de entrada una secuencia de enteros S del primer modelo de acceso secuencial y entero N, devuelva un valor booleano verdadero si en la secuencia S existe algún múltiplo de N, y falso en caso contrario.

Por ejemplo, si N es 3 y la secuencia S es (31, 5, 33, 45, 33, 15, 13, 15, 15) la función devuelve verdadero.

Es claramente un algoritmo de búsqueda muy sencillo, la única complicación posible es la comprobación de la propiedad, que podemos obtener fácilmente utilizando estableciendo la condición de terminación y negándola.

```pascal
function ExisteMultiploN(S : mse1; N : integer) : boolean;
begin
	comenzar_mse1(S);

	while (ea_mse1(S) <> mse1_marcaFin) and (ea_mse1(S) MOD N <> 0) do
		avanzar_mse1(S);

	ExisteMultiploN := ea_mse1(S) <> mse1_marcaFin;
end;
```
