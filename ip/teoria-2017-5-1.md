# Examen Teórico - 1 de Junio de 2017

# Ejercicio 1

## Apartado A

> Disponemos de una secuencia S de enteros del primer modelo de acceso secuencial en la que hay almacenados valores enteros. Diseñe un algoritmo que accediendo sólo una vez a cada elemento de la secuencia obtenga una nueva secuencia T donde se hayan eliminado los elementos repetidos de las mesetas de la secuencia S, o dicho de otra forma, que se registre un único elemento de cada meseta que tenga la secuencia S.

Por ejemplo, dada la secuencia S = (31, 31, 31, 45, 22, 22, 33, 15, 15, 15, 15), la solución es T = (31, 45, 22, 33, 15)

Para saber cuándo cambiamos de meseta, almacenamos el valor de la meseta actual y cuando `EA` sea diferente de ésta, la actualizamos y registramos `EA`. Tratamos el primer elemento fuera del bucle para poder inicializar la variable `actual`.

```pascal
program teoria2017junio1_1a;

uses unitmse1;

var
	S, T : mse1;
	actual : integer;

begin
	encender_maquina_mse1(S);
	cargar_fichero_mse1(S, 'numeros_repetidos.txt');
	comenzar_mse1(S);

	encender_maquina_mse1(T);
	crear_mse1(T);

	if ea_mse1(S) <> mse1_marcaFin then begin
		actual := ea_mse1(S);

		registrar_mse1(T, actual);

		avanzar_mse1(S);

		while ea_mse1(S) <> mse1_marcaFin do begin
			if ea_mse1(S) <> actual then begin
				actual := ea_mse1(S);
				registrar_mse1(T, actual);
			end;

			avanzar_mse1(S);
		end;
	end;

	marcar_mse1(T);
	salvar_fichero_mse1(T, 'solucion_1a.txt');
end.
```

## Apartado B

> Diseñe una función ExisteValorEntreNyM que teniendo como parámetros de entrada una secuencia de enteros S del segundo modelo de acceso secuencial y dos enteros N y M, con N menor que M (N < M), devuelva un valor booleano verdadero si en la secuencia S existe algún valor elem entre N y M (N < elem < M), y falso en caso contrario.

Por ejemplo, si N=3, M=6 y la secuencia S es (31 5 33 45 33 15 13 15 15), la función devuelve verdadero, puesto que en la secuencia existe un valor, el número 5, que está entre 3 y 6.

Es claramente un algoritmo de búsqueda en S. La complicación es averiguar la condición de continuación del bucle, así que apuntamos la de terminación (que es más sencilla de ver) y la negamos. De esta forma, sabemos que el bucle termina cuando:
* `EA` es la marca de fin
* `EA > N` y `EA < M`

Obtenemos pues la condición de continuación: `(EA <> marcaFin) Y [(EA < N) O (EA > M)]`

Es importante ver que cuando comprobamos la propiedad al final, debemos poner la condición de terminación, no la de continuación.

```pascal
function ExisteValorEntreNyM(S : mse1; N, M : integer) : boolean;
begin
	comenzar_mse1(S);

	while (ea_mse1(S) <> mse1_marcaFin) and ((ea_mse1(S) < N) or (ea_mse1(S) > M)) do
		avanzar_mse1(S);

	ExisteValorEntreNyM := (ea_mse1(S) <> mse1_marcaFin) and (ea_mse1(S) > N) and (ea_mse1(S) < M);
end;
```

# Ejercicio 2

En las guías docentes de un título del Grado en Informática con NA asignaturas, se pueden incluir entre 1 y los NME diferentes métodos de evaluación existentes.

## Apartado A

> Defina el tipo MétodosEvaluación. Debe ser un tipo de datos que tenga como dominio NME métodos de evaluación.

En estos casos el tipo ideal es un enumerado con los distintos métodos, pero como no los conocemos a priori, utilizaremos un tipo subrango de enteros desde 1 hasta NME.

```pascal
type
	MetodosEvaluacion = 1..NME;
```

## Apartado B

> Defina el tipo Asignaturas. Debe ser un tipo de datos que tenga como dominio las NA asignaturas de un título del Grado en Informática.

Nos encontramos en la misma situación que el apartado anterior, por tanto seguiremos la misma estrategia.

```pascal
type
	Asignaturas = 1..NA;
```

## Apartado C

> Defina el tipo MétodoEvaluaciónAsignatura. Debe ser un tipo adecuado para representar el uso o no de cada método de evaluación en cada asignatura. Este tipo debe definirse de forma tal que si se dispone de una variable de este tipo, a través de esa variable debe ser posible conocer si cualquier asignatura existente en el Grado incluye o no cualquier método de evaluación posible.

Se trata de definir una tabla, los índices suelen ser en estos casos los tipos que hemos definido previamente, y en el propio enunciado nos indican el orden en el que tenemos que declararlos.

Para el tipo base, sabemos que el uso de un método es un estado con dos valores posibles: se usa o no se usa; disponemos de un tipo perfecto para almacenar esto: booleano.

```pascal
type
	MetodoEvaluacionAsignatura = array [Asignaturas, MetodosEvaluacion] of boolean;
```

## Apartado D

> Defina la función AsignaturaMayorMetEval. Esta función tiene un parámetro de entrada de tipo MétodoEvaluaciónAsignatura y devuelve un valor de tipo `Asignaturas` con la asignatura que más métodos de evaluación utiliza. En el caso de existir más de una asignatura que cumpla esta condición, la función devolverá la primera que encuentre.

Debemos recorrer la tabla de dos dimensiones. Dentro del cuerpo del bucle tenemos que contar los métodos que utiliza cada asignatura, para ello definimos una variable que almacene la suma de métodos del elemento actual, otra del elemento con más métodos y otra con la asignatura que más métodos utiliza hasta el elemento actual.

```pascal
function AsignaturaMayorMetEval(tabla : MetodoEvaluacionAsignatura) : Asignaturas;
var
	a, asignaturaMayor : Asignaturas;
	m : MetodosEvaluacion;
	numActual, numViejo : integer;

begin
	asignaturaMayor := 1;
	numActual := 0;
	numViejo := 0;

	for a := 1 to NA do begin
		numActual := 0;

		for m := 1 to NME do begin
			if tabla[a][m] then
				numActual := numActual + 1;
		end;

		if numActual > numViejo then begin
			numViejo := numActual;
			asignaturaMayor := a;
		end;
	end;

	AsignaturaMayorMetEval := asignaturaMayor;
end;
```

## Apartado E

> Defina la función MétodoEvaluaciónInfrautilizado. Esta función tiene un parámetro de entrada de tipo MétodoEvaluaciónAsignatura y devuelve un valor booleano que indique si algún método de evaluación no es utilizado en ninguna asignatura.

Hay varios enfoques posibles respecto a este problema. Uno de ellos consiste en crear una tabla con índices MetodosEvaluacion y tipo base booleano, de forma que podamos recorrer todas las asignaturas de la tabla que nos pasan por parámetros, poniendo como verdaderos los métodos usados conforme los vayamos encontrando, y recorriendo nuestra tabla fuera del bucle para comprobar si alguno de los métodos está como falso.

El problema con este enfoque es que no es eficiente, en su lugar el que nosotros seguiremos es el siguiente:

Vamos a utilizar dos bucles, en el externo recorreremos cada método, y en el interno buscaremos una asignatura que lo utilice. De esta forma, cada vez que encontremos una asignatura que utilice el método, podremos pasar a buscar en el siguiente método.

```pascal
function MetodoEvaluacionInfrautilizado(tabla : MetodoEvaluacionAsignatura) : boolean;
var
	a : Asignaturas;
	m : MetodosEvaluacion;
	esUtilizado : boolean;

begin
	m := 1;
	esUtilizado := true;

	while (m <= NME) and esUtilizado do begin
		esUtilizado := false;

		a := 1;

		while (a <= NA) and not esUtilizado do begin
			if tabla[a][m] then
				esUtilizado := true;

			a := succ(a);
		end;

		m := succ(m);
	end;

	MetodoEvaluacionInfrautilizado := not esUtilizado;
end;
```

# Ejercicio 3

> Se define la siguiente sucesión por recurrencia:
>
> $ a_{0} = 1 $  
> $ a_{1} = 1 $  
> $ a_{i} = 7 \cdot a_{i-1} - a_{i-2} - 2\ (Para\ i \gt 1) $
>
> Escriba un algoritmo que calcule el valor de i más pequeño, con i > 1, tal que $a_{i}$ es superior a un número N dado.

Utilizaremos la misma estrategia que para la sucesión de Fibonacci: almacenamos el último y el penúltimo elemento en variables auxiliares y los utilizamos para calcular el elemento actual. El problema se reduce entonces a un esquema de búsqueda.

```pascal
program teoria2017junio1_3;

var
	N, penultimo, ultimo, actual, i : longint;

begin
	write('Introduzca N: ');
	readln(N);

	penultimo := 1;
	ultimo := 1;
	actual := 0;

	i := 1;

	while actual < N do begin
		i := i + 1;

		actual := 7 * ultimo - penultimo - 2;
		penultimo := ultimo;
		ultimo := actual;
	end;

	writeln('Encontrado: ', i, ' (', actual, ')');
end.
```